plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2' apply false
    id 'maven-publish'
    id 'signing'
    id 'org.openjfx.javafxplugin' version '0.1.0' apply false
}
group = 'pro.gravit.launcher'
version = '5.6.9'

apply from: 'props.gradle'

configure(subprojects.findAll { it.name != 'modules' }) {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'java'
    apply plugin: 'java-library'


    group = 'pro.gravit'

    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/groups/public' }
        maven {
            url "https://clojars.org/repo/"
        }
        maven {
            url "https://jcenter.bintray.com/"
        }
        maven {
            url "https://jitpack.io/"
        }
        maven {
            url 'https://maven.gravit-support.ru/repository/jitpack'
            credentials {
                username = 'gravitlauncher'
                password = 'gravitlauncher'
            }
        }
    }

    jar {
        duplicatesStrategy = 'EXCLUDE'
    }

    eclipse {
        classpath {
            downloadSources = true
            downloadJavadoc = true
        }
    }

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.encoding = "UTF-8"
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
            options.incremental = true // one flag, and things will get MUCH faster
            compileJava.options.encoding = 'UTF-8'
            compileTestJava.options.encoding = 'UTF-8'
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

defaultTasks 'build'
if (hasProperty('mavenUsername') && hasProperty('mavenPassword') && hasProperty('mavenReleaseRepository') && hasProperty('mavenSnapshotRepository')) {
    publishing {
        repositories {
            maven {
                url = version.endsWith('SNAPSHOT') ? getProperty('mavenSnapshotRepository') : getProperty('mavenReleaseRepository')
                credentials {
                    username getProperty('mavenUsername')
                    password getProperty('mavenPassword')
                }
            }
        }
    }
}
