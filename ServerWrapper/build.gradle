apply plugin: 'com.github.johnrengelman.shadow'

String mainClassName = "pro.gravit.launcher.server.ServerWrapper"
String mainAgentName = "pro.gravit.launcher.server.ServerAgent"

configurations {
    pack
    api.extendsFrom pack
}

repositories {
    maven {
        url "https://repo.spring.io/plugins-release/"
    }
}

sourceCompatibility = '17'
targetCompatibility = '17'

jar {
    archiveClassifier.set('clean')
    manifest.attributes("Main-Class": mainClassName,
            "Premain-Class": mainAgentName,
            "Can-Redefine-Classes": "true",
            "Can-Retransform-Classes": "true",
            "Can-Set-Native-Method-Prefix": "true",
            "Multi-Release": "true")
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
}

tasks.register('javadocJar', Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

dependencies {
    pack project(':LauncherAPI')
}

shadowJar {
    duplicatesStrategy = 'EXCLUDE'
    archiveClassifier = null
    configurations = [project.configurations.pack]
    exclude 'module-info.class'
}

publishing {
    publications {
        serverwrapperapi(MavenPublication) {
            artifactId = 'serverwrapper-api'
            artifact(jar) {
                classifier ""
            }
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'GravitLauncher ServerWrapper API'
                description = 'GravitLauncher ServerWrapper Module API'
                url = 'https://gravitlauncher.com'
                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'gravita'
                        name = 'Gravita'
                        email = 'gravita@gravit.pro'
                    }
                    developer {
                        id = 'zaxar163'
                        name = 'Zaxar163'
                        email = 'zahar.vcherachny@yandex.ru'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/GravitLauncher/Launcher.git'
                    developerConnection = 'scm:git:ssh://git@github.com:GravitLauncher/Launcher.git'
                    url = 'https://gravitlauncher.com/'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.serverwrapperapi
}

assemble.dependsOn tasks.shadowJar
