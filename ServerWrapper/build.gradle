apply plugin: 'com.github.johnrengelman.shadow'

String mainClassName = "pro.gravit.launcher.server.ServerWrapper"
String mainAgentName = "pro.gravit.launcher.server.ServerAgent"

configurations {
    pack
    compile.extendsFrom pack
}

repositories {
    maven {
        url "http://repo.spring.io/plugins-release/"
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

jar {
    classifier = 'clean'
    manifest.attributes("Main-Class": mainClassName,
            "Premain-Class": mainAgentName,
            "Can-Redefine-Classes": "true",
            "Can-Retransform-Classes": "true",
            "Can-Set-Native-Method-Prefix": "true")
}

dependencies {
    pack project(':LauncherAuthlib')
    pack 'org.apache.httpcomponents:httpclient:4.5.7'
    pack 'io.netty:netty-codec-http:4.1.36.Final'
}

shadowJar {
    classifier = null
    relocate 'io.netty', 'pro.gravit.repackage.io.netty'
    configurations = [project.configurations.pack]
    exclude 'module-info.class'
}


build.dependsOn tasks.shadowJar
 