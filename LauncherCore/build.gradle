sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {
    compileOnly group: 'org.fusesource.jansi', name: 'jansi', version: rootProject['verJansi']
    compileOnly group: 'org.jline', name: 'jline', version: rootProject['verJline']
    compileOnly group: 'org.jline', name: 'jline-reader', version: rootProject['verJline']
    compileOnly group: 'org.jline', name: 'jline-terminal', version: rootProject['verJline']
    compileOnly group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: rootProject['verBcprov']
    compileOnly group: 'org.slf4j', name: 'slf4j-api', version: rootProject['verSlf4j']
    api group: 'com.google.code.gson', name: 'gson', version: rootProject['verGson']
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: rootProject['verJunit']
    testImplementation group: 'org.jline', name: 'jline', version: rootProject['verJline']
    testImplementation group: 'org.jline', name: 'jline-reader', version: rootProject['verJline']
    testImplementation group: 'org.jline', name: 'jline-terminal', version: rootProject['verJline']
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
sourceSets {
    java11 {
        java {
            srcDirs = ['src/main/java11']
        }
        dependencies {
            java11Implementation group: 'com.google.code.gson', name: 'gson', version: rootProject['verGson']
            java11Implementation files(sourceSets.main.output.classesDirs) { builtBy compileJava }
        }
    }
}
jar {
    into('META-INF/versions/11') {
        from sourceSets.java11.output
    }
    archiveClassifier.set('clean')
}
compileJava11Java {
    sourceCompatibility = 11
    targetCompatibility = 11
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

publishing {
    publications {
        launchercore(MavenPublication) {
            artifactId = 'launcher-core'
            artifact(jar) {
                classifier ""
            }
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'GravitLauncher Core Utils'
                description = 'GravitLauncher Core Utils'
                url = 'https://launcher.gravit.pro'
                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'gravita'
                        name = 'Gravita'
                        email = 'gravita@gravit.pro'
                    }
                    developer {
                        id = 'zaxar163'
                        name = 'Zaxar163'
                        email = 'zahar.vcherachny@yandex.ru'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/GravitLauncher/Launcher.git'
                    developerConnection = 'scm:git:ssh://git@github.com:GravitLauncher/Launcher.git'
                    url = 'https://launcher.gravit.pro/'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.launchercore
}
