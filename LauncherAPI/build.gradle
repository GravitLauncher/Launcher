sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {
    api project(':LauncherCore')
    compileOnly group: 'io.netty', name: 'netty-codec-http', version: rootProject['verNetty']
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: rootProject['verJunit']
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

sourceSets {
    java11 {
        java {
            srcDirs = ['src/main/java11']
        }
        dependencies {
            java11Implementation files(sourceSets.main.output.classesDirs) { builtBy compileJava }
        }
    }
}

jar {
    into('META-INF/versions/11') {
        from sourceSets.java11.output
    }
    archiveClassifier.set('clean')
}

compileJava11Java {
    sourceCompatibility = 11
    targetCompatibility = 11
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

publishing {
    publications {
        launcherwsapi(MavenPublication) {
            artifactId = 'launcher-ws-api'
            artifact(jar) {
                classifier ""
            }
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'GravitLauncher WebSocket API'
                description = 'GravitLauncher WebSocket Module API'
                url = 'https://launcher.gravit.pro'
                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'gravita'
                        name = 'Gravita'
                        email = 'gravita@gravit.pro'
                    }
                    developer {
                        id = 'zaxar163'
                        name = 'Zaxar163'
                        email = 'zahar.vcherachny@yandex.ru'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/GravitLauncher/Launcher.git'
                    developerConnection = 'scm:git:ssh://git@github.com:GravitLauncher/Launcher.git'
                    url = 'https://launcher.gravit.pro/'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.launcherwsapi
}
